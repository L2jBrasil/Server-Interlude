plugins {
    id "java"
    id "idea"
    id "eclipse"
    id "application"
}
ext.moduleName = 'com.l2jbr.loginserver'
mainClassName = "com.l2jbr.loginserver.L2LoginServer"

version '1.4'

sourceSets {
    main {
        java {
            srcDirs = ['src/main/com.l2jbr.loginserver']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

dependencies {
    compile project(':Commons')
    compile project(':Mmocore')

    compile 'org.slf4j:slf4j-api:1.8.0-beta2'
    compile 'org.springframework.data:spring-data-jdbc:1.0.0.M3'

    runtime 'org.apache.logging.log4j:log4j-core:2.11.0'
    runtime 'org.apache.logging.log4j:log4j-api:2.11.0'
    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.0'
    runtime 'com.zaxxer:HikariCP:3.2.0'
    runtime ('mysql:mysql-connector-java:8.0.11') {
        transitive = false
    }
}


def generalManifest = manifest {
    attributes('Built-By': System.getProperty('user.name'),
            'Built-Date': new Date().format("yyyy-MM-dd HH:mm:ss"))
}

jar {
    baseName 'l2j-loginserver'

    exclude('config/**')
    exclude('*.cfg')

    manifest {
        from(generalManifest)
        attributes('Main-Class': 'com.l2jbr.loginserver.L2LoginServer')
    }
}

task zip(type: Zip, group: "Archive", description: "Zip Loginserver") {
    into('lib') {
        from([jar, configurations.runtime])
    }

    from('bin')
    from(sourceSets.main.resources)

    destinationDir project.buildDir
}

build.finalizedBy(zip)
